class_name PlayerResourcesSystem
extends RefCounted


static func step(state: PlayerState, config: SimConfig) -> void:
	state.health_recovery_delay_ticks = maxi(0, state.health_recovery_delay_ticks - 1)
	state.flux_recovery_delay_ticks = maxi(0, state.flux_recovery_delay_ticks - 1)
	if state.health > 0 and state.health_recovery_delay_ticks == 0:
		_apply_health_rate(state, PlayerTuning.HEALTH_RECOVERY_PER_SECOND, config)
	if state.flux_recovery_delay_ticks == 0:
		_apply_flux_rate(state, PlayerTuning.FLUX_RECOVERY_PER_SECOND, config)


static func damage(state: PlayerState, amount: int, config: SimConfig) -> bool:
	if amount <= 0 or state.health <= 0:
		return false
	state.health = maxi(0, state.health - amount)
	state.health_recovery_remainder = 0
	state.health_recovery_delay_ticks = config.milliseconds_to_ticks(PlayerTuning.HEALTH_RECOVERY_DELAY_MS)
	return true


static func spend_flux(state: PlayerState, amount: int, config: SimConfig) -> bool:
	if amount <= 0 or state.flux < amount:
		return false
	state.flux -= amount
	state.flux_recovery_remainder = 0
	state.flux_recovery_delay_ticks = config.milliseconds_to_ticks(PlayerTuning.FLUX_RECOVERY_DELAY_MS)
	return true


static func _apply_health_rate(state: PlayerState, rate_per_second: int, config: SimConfig) -> void:
	var total: int = state.health_recovery_remainder + rate_per_second
	@warning_ignore("integer_division")
	var amount: int = total / config.tick_rate
	state.health_recovery_remainder = total - amount * config.tick_rate
	state.health = clampi(state.health + amount, 0, PlayerTuning.HEALTH_MAXIMUM)


static func _apply_flux_rate(state: PlayerState, rate_per_second: int, config: SimConfig) -> void:
	var total: int = state.flux_recovery_remainder + rate_per_second
	@warning_ignore("integer_division")
	var amount: int = total / config.tick_rate
	state.flux_recovery_remainder = total - amount * config.tick_rate
	state.flux = clampi(state.flux + amount, 0, PlayerTuning.FLUX_MAXIMUM)
